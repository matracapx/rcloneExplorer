apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    signingConfigs {
        github_x0b {
            keyAlias 'github_x0b'
        }
    }
    compileSdkVersion 29
    defaultConfig {
        applicationId 'ca.pkay.rcloneexplorer.x0b'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 7
        versionName '1.10.2'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField "java.lang.long", "BUILD_TIME", String.valueOf(System.currentTimeMillis()) + "L"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "java.lang.long", "BUILD_TIME", String.valueOf(System.currentTimeMillis()) + "L"
        }
    }

    sourceSets {
        all {
            jniLibs.srcDirs = ["lib"]
        }
    }

    flavorDimensions 'edition'
    productFlavors {

        // App id for play store
        rcx {
            dimension 'edition'
            applicationId 'io.github.x0b.rcx'
        }

        // App id for github
        rcloneExplorer {
            dimension 'edition'
            applicationId 'ca.pkay.rcloneexplorer.x0b'
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk true
        }
    }

    project.ext.versionCodes = [
            'armeabi-v7a': 6,
            'arm64-v8a': 7,
            'x86': 8,
            'x86_64': 9]

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) + android.defaultConfig.versionCode * 10
        }
    }
}

repositories {
    mavenCentral()
    google()
    jcenter()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.core:core:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.leinardi.android:speed-dial:2.0.0'
    implementation 'us.feras.mdv:markdownview:1.1.0'
    implementation 'jp.wasabeef:recyclerview-animators:2.3.0'
    implementation 'com.github.GrenderG:Toasty:1.3.0'
    // Firebase & Crashlytics
    implementation 'com.google.firebase:firebase-core:17.2.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    // Thumbnails
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    // REST Client
    implementation 'com.squareup.okhttp3:okhttp:4.2.0'
    debugImplementation 'com.squareup.okhttp3:logging-interceptor:4.2.0'
    // JSON
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.9'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.9'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.9'
    // Timestmps
    implementation 'com.github.x0b:rfc3339parser:2.0.0'
    // SAF/WebDAV
    implementation project(':safdav')
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    // TEST
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha02'
}

apply plugin: 'com.google.gms.google-services'
